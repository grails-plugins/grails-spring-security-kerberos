buildscript {
	ext {
		grailsVersion = project.grailsVersion
	}
	repositories {
		mavenLocal()
		maven { url 'https://repo.grails.org/grails/core' }
	}
	dependencies {
		classpath "org.grails:grails-gradle-plugin:$grailsVersion"
	}
}

plugins {
	id 'io.spring.dependency-management' version '0.5.4.RELEASE'
}

apply plugin: 'spring-boot'
apply from: '../gradle/testapp.gradle'

dependencies {

	compile 'org.apache.httpcomponents:httpclient:4.5.1'

	String apacheDirServerVersion = '2.0.0-M15'
	['core-api', 'interceptor-kerberos', 'protocol-shared', 'protocol-kerberos',
	 'ldif-partition', 'mavibot-partition', 'jdbm-partition', 'protocol-ldap'].each {
		compile "org.apache.directory.server:apacheds-$it:$apacheDirServerVersion", {
			exclude module: 'api-ldap-schema-data'
		}
	}

	String apacheDirApiVersion = '1.0.0-M20'
	compile "org.apache.directory.api:api-all:$apacheDirApiVersion", {
		['api-ldap-schema-data', 'xml-apis', 'xpp3', 'dom4j'].each { exclude module: it }
	}
}

integrationTest {
	beforeTest { descriptor -> logger.quiet " -- $descriptor" }
}
